;;
;; .sawfishrc -- Sawfish window manager configuration file
;; Copyright 2008 by Michal Nazarewicz
;;
;; This program is free software: you can redistribute it and/or
;; modify it under the terms of the GNU General Public License as
;; published by the Free Software Foundation, either version 3 of the
;; License, or (at your option) any later version.
;;
;; This program is distributed in the hope that it will be useful, but
;; WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
;; General Public License for more details.
;;
;; You should have received a copy of the GNU General Public License
;; along with this program.  If not, see <http://www.gnu.org/licenses/>.
;;

;;{{{ What machine are we running

(defvar mn-machine
  (catch 'return
    (let ((sn (system-name)))
      (mapc (lambda (spec)
              (when (string-looking-at (car spec) sn)
                (throw 'return (cdr spec))))
            '(("erwin"     . erwin)
              ("tuptus"    . tuptus)
              ("pikus"     . pikus))))))

(defun case-equal (value #!rest list)
  (while (and list (caar list) (not (member value (caar list))))
    (setq list (cdr list)))
  (and list (cadar list)))


(defun screen-size ()
  "Screen resolution formatted as WIDTHxHEIGHT."
  (concat (number->string (screen-width)) "x" (number->string (screen-height))))

;;}}}
;;{{{ Misc small

;; Show errors on screen
(require 'sawfish.wm.ext.error-handler)
(setq error-destination 'screen)


;; Hyper (win key) as prefix
(custom-set-typed-variable
 (quote wm-modifier-value) (quote (super)) (quote modifier-list))


;; Focus and Auto Raise
(require 'auto-raise)
(setq focus-mode             'enter-only
      focus-click-through    t
      raise-windows-on-focus t
      raise-window-timeout   200)
(bind-keys window-keymap
           "W-Button2-Click"(lambda () (raise-lower-window   (input-focus))))


;; Moving and resizing
(require 'sawfish.wm.commands.move-resize)
(setq move-show-position     t
      resize-show-dimensions t
      move-outline-mode      'box
      resize-outline-mode    'box
      move-snap-epsilon      5
      move-snap-mode         'magnetism)

(bind-keys window-keymap
           "W-Button1-Move" (lambda()(move-window-interactively  (input-focus)))
           "W-Button3-Move" (lambda()(resize-window-interactively(input-focus))))


;; No decoration
(add-hook 'before-add-window-hook
          (lambda (w)
            (unless (window-transient-p w)
              (set-window-type w 'unframed))))

;;}}}
;;{{{ Viewports

(set-number-of-viewports 10 1)
(set-screen-viewport 0 0)

(bind-keys global-keymap
           "W-F1"           (lambda () (set-screen-viewport 0 0))
           "W-F2"           (lambda () (set-screen-viewport 1 0))
           "W-F3"           (lambda () (set-screen-viewport 2 0))
           "W-F4"           (lambda () (set-screen-viewport 3 0))
           "W-F5"           (lambda () (set-screen-viewport 4 0))
           "W-F6"           (lambda () (set-screen-viewport 5 0))
           "W-F7"           (lambda () (set-screen-viewport 6 0))
           "W-F8"           (lambda () (set-screen-viewport 7 0))
           "W-F9"           (lambda () (set-screen-viewport 8 0))
           "W-F10"          (lambda () (set-screen-viewport 9 0)))

(defmacro set-current-window-viewport-x-0 (x)
  (list 'lambda nil (list 'set-window-viewport (list 'input-focus) x '0)))

(bind-keys window-keymap
           "W-1"            (set-current-window-viewport-x-0 0)
           "W-2"            (set-current-window-viewport-x-0 1)
           "W-3"            (set-current-window-viewport-x-0 2)
           "W-4"            (set-current-window-viewport-x-0 3)
           "W-5"            (set-current-window-viewport-x-0 4)
           "W-6"            (set-current-window-viewport-x-0 5)
           "W-7"            (set-current-window-viewport-x-0 6)
           "W-8"            (set-current-window-viewport-x-0 7)
           "W-9"            (set-current-window-viewport-x-0 8)
           "W-0"            (set-current-window-viewport-x-0 9))

;;}}}
;;{{{ Transparency

(defvar default-window-alpha (cons 0.9 0.7))

(defconst window-alpha-max   #xffffffff)
(defconst window-alpha-min   #x1fffffff)

;; If alpha is a list then:
;; * if blur is nil     takes alpha's car,
;; * if blur is not nil takes alpha's cdr.
;; If now alpha is not a number returns `window-alpha-max'.
;; If alpha is within [0, 1] multiplies it by `window-alpha-max'
;; Returns (max `window-alpha-min' (min `window-alpha-max' alpha))
(defun window-alpha-value (alpha blur)
  (let ((a (if (listp alpha) (if blur (cdr alpha) (car alpha)) alpha)))
    (if (numberp a)
        (clamp (or (and (>= a 0) (<= a 1) (* a window-alpha-max)) a)
               window-alpha-min window-alpha-max)
      window-alpha-max)))

;; Return's window's alpha property or `default-window-alpha'
(defun window-alpha-property (w)
          (or (window-get w 'alpha) default-window-alpha))

;; Sets window opacity.
;; If alpha is nil uses `window-alpha-property'.
;; Uses `window-alpha-value' to get alpha value from alpha and blur arguments.
(defun set-window-alpha (w alpha #!optional blur)
  (let ((a (window-alpha-value (or alpha (window-alpha-property w)) blur)))
    (if (= a window-alpha-max)
        (delete-x-property (window-frame-id w) '_NET_WM_WINDOW_OPACITY))
    (set-x-property (window-frame-id w) '_NET_WM_WINDOW_OPACITY
                    (make-vector 1 a) 'CARDINAL 32))
  (sync-server))

(add-hook 'focus-in-hook  (lambda (w fmode) (set-window-alpha w nil nil)))
(add-hook 'focus-out-hook (lambda (w fmode) (set-window-alpha w nil t  )))

;;}}}
;;{{{ Applications & menus

(require 'menus)
(require 'customize)

(defvar mn-term-command
  (case-equal (screen-size)
    '(("1680x1050" "1920x1080" "1600x1200") "term -N Term -g 80x35" )
    '(("1024x768" "1024x600")               "term -N Term -g 80x25" )
    '(("1200x1600" "2400x1600")             "term -N Term -g 80x150")
    '(("3600x1080")                         "term -N Term -g 80x105")
    '(()                                    "term -N Term -g 80x25" )))
(defvar mn-emacs-command
  "if ! emacsclient -c -n; then emacs --daemon; emacsclient -c -n; fi")
(let ((w (if (equal "3600x1080" (screen-size)) "117" "80")))
  (defvar mn-mpdshow-command
    (concat "term -N MPD -mesg -g " w "x1 -e mpd-show"))
  (defvar mn-irssi-command
    (concat "term -N IRC -mesg -g " w "x10 -e "
            (if (eq mn-machine 'pikus) "ssh -D 12312 live" "irssi"))))


;; Jump to term or exec
(require 'sawfish.wm.util.window-order)
(let (;; Inspired by jump-or-exec written by Damien Elmes <resolve@repose.cx>
      (mn-jump-or-exec
       (lambda (regexp command)
         (let ((w (get-window-by-name-re regexp)))
           (if w (display-window w) (system (concat command " &")))))))

  ;; Menus
  (setq apps-menu
        '(("_irssi"     (mn-jump-or-exec "^IRC"     mn-irssi-command))
          ("_Emacs"     (mn-jump-or-exec "^Emacs"   mn-emacs-command))
          ("The _GIMP"  (mn-jump-or-exec "^GIMP$"   "gimp"))
          ("GQ_View"    (mn-jump-or-exec "^GQview$" "gqview"))
          ("_Opera"     (mn-jump-or-exec "Opera$"   "opera")))
        root-menu
        (nconc
         '(("_Customize" customize)
           ("Re_start"   restart)
           ()
           ("_Quit"      quit)
           ("_Reboot"    (progn (system "( sleep 2; /sbin/reboot )") (quit))))
         (and (file-exists-p "/usr/local/sbin/reboot-w")
              '(("Reboot _Windows"
                 (progn (system "( sleep 2; /usr/local/sbin/reboot-w )") (quit)))))
         '(("Power _Off" (progn (system "( sleep 2; /sbin/halt   )") (quit))))))


  ;; Application and menu bindings
  (bind-keys
   root-window-keymap
   "Button1-Click2" (lambda () (system (concat mn-term-command " &")))
   "Button2-Click"  (lambda () (popup-menu window-menu))
   "Button3-Click"  popup-apps-menu
   "W-Space"        popup-apps-menu
   "W-Button3-Click"popup-root-menu)

  (bind-keys
   global-keymap
   "W-`"
   (lambda ()
     (catch 'term
       (mapc
        (lambda (w)
          (and (not (window-get w 'sticky))
               (let ((cls (get-x-text-property w 'WM_CLASS)))
                 (and cls (string-match "xterm|u?rxvt|Term" (aref cls 0))))
               (throw 'term (display-window w))))
        (window-order current-workspace t nil))
       (system (concat mn-term-command " &"))))

   "W-q"           (lambda()(mn-jump-or-exec "^Emacs"   mn-emacs-command))
   "XF86HomePage"  (lambda()(mn-jump-or-exec "Opera$"   "opera"))
   "XF86Search"    (lambda()(system "opera http://google.com/ &"))
   "W-w"           (lambda()(mn-jump-or-exec "Opera$"   "opera"))
   "W-e"           (lambda()(mn-jump-or-exec "^IRC"      mn-irssi-command))
   "W-a"           (lambda()(mn-jump-or-exec "^GIMP$"   "gimp"))
   "W-s"           (lambda()(mn-jump-or-exec "^GQview$" "gqview"))

   "XF86Mail"
   (lambda ()
     (mn-jump-or-exec "^Emacs" mn-emacs-command)
     (system "emacsclient -ne '(if (fboundp '\\''switch-to-gnus-or-get-new-news) (switch-to-gnus-or-get-new-news))'"))
   "XF86Calculator"
   (lambda ()
     (mn-jump-or-exec "^Emacs"   mn-emacs-command)
     (system "emacsclient -a '' -ne '(calc)'"))

   "XF86AudioMute"
   (lambda () (system "/usr/bin/amixer set Master\\ Front toggle"))
   "XF86AudioLowerVolume"
   (lambda () (system "/usr/bin/amixer set Master\\ Front 5%-"))
   "XF86AudioRaiseVolume"
   (lambda () (system "/usr/bin/amixer set Master\\ Front 5%+")))

  (bind-keys
   window-keymap
   "W-Space"        (lambda () (popup-window-menu (input-focus)))))

;; Run some applications
(add-hook 'after-initialization-hook
          (lambda ()
            (mapc (lambda (spec)
                    (let ((cnd  (car  spec))
                          (name (cadr spec))
                          (cmd  (cddr spec)))
                      (and cmd
                           (or (not cnd) (member mn-machine cnd))
                           (not (and name (get-window-by-name-re name)))
                           (system (concat cmd " &")))))
                  `(((erwin pikus)       "Opera"    . "opera")
                    (()                  "Emacs"    . ,mn-emacs-command)
                    ((erwin pikus)       "IRC"      . ,mn-irssi-command)
                    ((erwin pikus)       "mpdshow"  . ,mn-mpdshow-command)
                    (()                  "gkrellm"  . "gkrellm")))
            (set-screen-viewport 0 0)))

;;}}}
;;{{{ Music Player Daemon

(when (member mn-machine '(erwin tuptus pikus))
  (when (and (= mn-machine 'tuptus) (not (getenv "MPD_HOST")))
    (setenv "MPD_HOST" "erwin"))

  (defmacro mn-mpd-lambda (command)
    (list 'lambda () (list 'system (concat "mpc " command " >/dev/null"))))

  (bind-keys global-keymap
             "W-z"            (mn-mpd-lambda "prev")
             "W-x"            (mn-mpd-lambda "play")
             "W-c"            (mn-mpd-lambda "pause")
             "XF86AudioPlay"  (mn-mpd-lambda "toggle")
             "W-v"            (mn-mpd-lambda "next")))

;;}}}
;;{{{ Matcher

(require 'sawfish.wm.ext.match-window)

(define-match-window-setter 'location
  (lambda (wnd prop value)
    (declare (unused prop))
    (let ((x (nth 0 value)) (y (nth 1 value))
          (w (nth 2 value)) (h (nth 3 value))
          (v (nth 4 value)))
      ; Position
      (when (and x y)
        (when (< x 0) (setq x (+ (screen-width ) x)))
        (when (< y 0) (setq y (+ (screen-height) y)))
        (window-put wnd 'ignore-program-position #t)
        (move-window-to wnd x y))
      ; Dimensions
      (when (and w h)
        (resize-window-with-hints wnd w h))
      ; Viewport (maybe)
      (when v
        (set-screen-viewport     (1- v) 0)
        (set-window-viewport wnd (1- v) 0))
      ; Maximize (maybe)
      (when (and x w (= x 0) (= w (screen-width)))
        (window-put wnd 'queued-horizontal-maximize #t))
      (when (and y h (= y 0) (= h (screen-height)))
        (window-put wnd 'queued-horizontal-maximize #t)))))

(define-match-window-setter 'sticky-and-skip
  (lambda (w prop value)
    (declare (unused prop))
    (mapc (lambda (prp) (window-put w prp value))
          '(sticky sticky-viewport cycle-skip window-list-skip task-list-skip))))

(setq
 match-window-profile
 (let ((size (screen-size)))
   (mapcar (lambda (wnd-rule)
             (cons (car wnd-rule)
                   (apply nconc
                          (mapcar
                           (lambda (rule)
                             (and (string-match (car rule) size) (cdr rule)))
                           (cdr wnd-rule)))))
    '(;; Various terminals, size is specified via -g in command
      (((WM_CLASS . "/Term$"))
       ("1680x1050" (position  500 .   0))
       ("1024x768"  (position  480 . 100))
       ("1600x1200" (position  492 .   0))
       ("3200x1200" (position 1120 . 100))
       ("3600x1080" (position 2420 .   0)))

      (((WM_CLASS . "/IRC$"))
       ("1680x1050" (position 1132 .  10))
       ("1024x768"  (position  480 .  10))
       ("1600x1200" (position  492 .   0))
       ("3200x1200" (position 1120 .  10))
       (".*x1600"   (position  650 .  10))
       ("3600x1080" (position 2900 .  10))
       (""          (sticky . #t) (sticky-viewport . #t)))

      (((WM_CLASS . "/MPD$" ))
       ("1680x1050" (position 1132 .   0))
       ("1024x768"  (position  480 .   0))
       ("1600x1200" (position  492 .   0))
       ("3200x1200" (position 1120 .   0))
       (".*x1600"   (position  650 .   0))
       ("3600x1080" (position 2900 .   0))
       (""          (focus-when-mapped . #f) (focus-mode . click)
                    (sticky-and-skip . #t)))

      ;; Opera
      (((WM_CLASS . "^Opera/opera$"))
       ("1680x1050" (location  500  350   1179  700  1))
       ("1920x1080" (location  500  350   1419  730  1))
       ("1024x768"  (location    0  368   1024  400  1))
       ("1600x1200" (location  600  500   1000  700  1))
       ("1200x1600" (location    0    0   1200 1600  2))
       ("2400x1600" (location 1200    0   1200 1600  1))
       ("3600x1080" (location    0    0   1920 1080  1) (maximize . both)))

       ;; Emacs
      (((WM_CLASS . "^Emacs"))
       ("1680x1050" (location    0    0     80  105  1) (maximize . vertical))
       ("1920x1080" (location    0    0     80  108  1) (maximize . vertical))
       ("1024x768"  (location    0    0     80   76  1) (maximize . vertical))
       ("1600x1200" (location    0    0     80  120  1) (maximize . vertical))
       ("3200x1200" (location   72    0    170  118  1) (maximize . vertical))
       ("3600x1080" (location    0    0     80  105  1) (maximize . vertical))
       (".*x1600"   (location    0    0     80  160  1) (maximize . vertical)))

      ;; Some windows do not like alpha
      (((WM_CLASS . "^Gimp/|^MPlayer/|^openttd/|^Inkscape/"))
       (""          (alpha . 1)))

      ;; GIMP
      (((WM_CLASS . "^Gimp/gimp$") (WM_WINDOW_ROLE . "^gimp-[^s]"))
       (""          (viewport 2 . 1) (group . Gimp))
       ("1200x1600" (viewport 3 . 1)))

      (((WM_CLASS . "^Gimp/gimp$") (WM_WINDOW_ROLE . "^gimp-startup$"))
       (""          (place-mode . centered) (depth . 16)
                    (focus-when-mapped . #f) (never-focus . #t)
                    (sticky-and-skip . #t)))

       ;; GQview
      (((WM_ICON_NAME . "^GQview$"))
       (""          (viewport 3 . 1))
       ("1200x1600" (viewport 4 . 1)))))))
;;}}}

; (restart)
