;;
;; .sawfishrc -- Sawfish window manager configuration file
;; Copyright 2008 by Michal Nazarewicz
;;
;; This program is free software: you can redistribute it and/or
;; modify it under the terms of the GNU General Public License as
;; published by the Free Software Foundation, either version 3 of the
;; License, or (at your option) any later version.
;;
;; This program is distributed in the hope that it will be useful, but
;; WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
;; General Public License for more details.
;;
;; You should have received a copy of the GNU General Public License
;; along with this program.  If not, see <http://www.gnu.org/licenses/>.
;;

;;{{{ What machine are we running

(defvar mn-machine
  (catch 'return
    (let ((sn (system-name)))
      (mapc (lambda (spec)
              (when (string-looking-at (car spec) sn)
                (throw 'return (cdr spec))))
            '(("erwin"     . erwin)
              ("tuptus"    . tuptus)
              ("pikus"     . pikus))))))

(defun mn-val-filter (value #!rest list)
  (mapcar cdr (filter (lambda (element)
                        (let ((a (car element)))
                          (cond
                           ((not a)           t)
                           ((not (listp a))   (equal value a))
                           ((= (car a) 'eval) (eval (cdr a)))
                           (t                 (member value a)))))
                      list)))

(defun case-equal (value #!rest list)
  (while (and list (not (member value (caar list))))
    (setq list (cdr list)))
  (and list (cadar list)))


(defun screen-size ()
  "Screen resolution formatted as WIDTHxHEIGHT."
  (concat (number->string (screen-width)) "x" (number->string (screen-height))))

;;}}}
;;{{{ Misc small

;; Show errors on screen
(require 'sawfish.wm.ext.error-handler)
(setq error-destination 'screen)


;; Hyper (win key) as prefix
(custom-set-typed-variable
 (quote wm-modifier-value) (quote (hyper)) (quote modifier-list))


;; Focus and Auto Raise
(require 'auto-raise)
(setq focus-mode             'enter-only
      focus-click-through    t
      raise-windows-on-focus t
      raise-window-timeout   200)
(bind-keys window-keymap
           "W-Button2-Click"(lambda () (raise-lower-window   (input-focus))))


;; Moving and resizing
(require 'sawfish.wm.commands.move-resize)
(setq move-show-position     t
      resize-show-dimensions t
      move-outline-mode      'box
      resize-outline-mode    'box
      move-snap-epsilon      5
      move-snap-mode         'magnetism)

(bind-keys window-keymap
           "W-Button1-Move" (lambda()(move-window-interactively  (input-focus)))
           "W-Button3-Move" (lambda()(resize-window-interactively(input-focus))))


;; No decoration
(add-hook 'before-add-window-hook
          (lambda (w)
            (unless (window-transient-p w)
              (set-window-type w 'unframed))))

;;}}}
;;{{{ Viewports

(set-number-of-viewports 10 1)
(set-screen-viewport 0 0)

(bind-keys global-keymap
           "W-F1"           (lambda () (set-screen-viewport 0 0))
           "W-F2"           (lambda () (set-screen-viewport 1 0))
           "W-F3"           (lambda () (set-screen-viewport 2 0))
           "W-F4"           (lambda () (set-screen-viewport 3 0))
           "W-F5"           (lambda () (set-screen-viewport 4 0))
           "W-F6"           (lambda () (set-screen-viewport 5 0))
           "W-F7"           (lambda () (set-screen-viewport 6 0))
           "W-F8"           (lambda () (set-screen-viewport 7 0))
           "W-F9"           (lambda () (set-screen-viewport 8 0))
           "W-F9"           (lambda () (set-screen-viewport 9 0)))

(defmacro set-current-window-viewport-x-0 (x)
  (list 'lambda nil (list 'set-window-viewport (list 'input-focus) x '0)))

(bind-keys window-keymap
           "W-1"            (set-current-window-viewport-x-0 0)
           "W-2"            (set-current-window-viewport-x-0 1)
           "W-3"            (set-current-window-viewport-x-0 2)
           "W-4"            (set-current-window-viewport-x-0 3)
           "W-5"            (set-current-window-viewport-x-0 4)
           "W-6"            (set-current-window-viewport-x-0 5)
           "W-7"            (set-current-window-viewport-x-0 6)
           "W-8"            (set-current-window-viewport-x-0 7)
           "W-9"            (set-current-window-viewport-x-0 8)
           "W-0"            (set-current-window-viewport-x-0 9))

;;}}}
;;{{{ Transparency

(defvar default-window-alpha (cons 0.9 0.7))

(defconst window-alpha-max   #xffffffff)
(defconst window-alpha-min   #x1fffffff)

;; If alpha is a list then:
;; * if blur is nil     takes alpha's car,
;; * if blur is not nil takes alpha's cdr.
;; If now alpha is not a number returns `window-alpha-max'.
;; If alpha is within [0, 1] multiplies it by `window-alpha-max'
;; Returns (max `window-alpha-min' (min `window-alpha-max' alpha))
(defun window-alpha-value (alpha blur)
  (let ((a (if (listp alpha) (if blur (cdr alpha) (car alpha)) alpha)))
    (if (numberp a)
        (clamp (or (and (>= a 0) (<= a 1) (* a window-alpha-max)) a)
               window-alpha-min window-alpha-max)
      window-alpha-max)))

;; Return's window's alpha property or `default-window-alpha'
(defun window-alpha-property (w)
          (or (window-get w 'alpha) default-window-alpha))

;; Sets window opacity.
;; If alpha is nil uses `window-alpha-property'.
;; Uses `window-alpha-value' to get alpha value from alpha and blur arguments.
(defun set-window-alpha (w alpha #!optional blur)
  (let ((a (window-alpha-value (or alpha (window-alpha-property w)) blur)))
    (if (= a window-alpha-max)
        (delete-x-property (window-frame-id w) '_NET_WM_WINDOW_OPACITY))
    (set-x-property (window-frame-id w) '_NET_WM_WINDOW_OPACITY
                    (make-vector 1 a) 'CARDINAL 32))
  (sync-server))

(add-hook 'focus-in-hook  (lambda (w fmode) (set-window-alpha w nil nil)))
(add-hook 'focus-out-hook (lambda (w fmode) (set-window-alpha w nil t  )))

;;}}}
;;{{{ Applications & menus

(require 'menus)
(require 'customize)

(defvar mn-term-command
  (case-equal (screen-size)
    '(("1680x1050") "term -T term        -g 80x32+583+0" )
    '(("1024x768" ) "term -T term        -g 80x25-64+100")
    '(("1600x1200") "term -T term        -g 80x35+492+0" )
    '(("1200x1600") "term -T term        -g 80x35-72+100")))
(defvar mn-mpdshow-command
  (when (= mn-machine 'erwin)
    "term -T mpdshow -mesg -g 80x1-68+0 -e mpd-show -c80"))
(defvar mn-irssi-command
  (case-equal (screen-size)
    '(("1680x1050") "term -T irssi -mesg -g 80x10-68+10 -e irssi")
    '(("1024x768" ) "term -T irssi -mesg -g 80x10-64+0  -e irssi")
    '(("1600x1200") "term -T irssi -mesg -g 80x10-72+0  -e irssi")
    '(("1200x1600") "term -T irssi -mesg -g 80x10-72+0  -e irssi")))


;; Get terminal window
(require 'sawfish.wm.util.window-order)
(defun mn-focus-term ()
  (let ((windows
         (filter
          (lambda (w)
            (and (not (= w (input-focus)))
                 (not (window-get w 'sticky))
                 (string-equal "xterm" (aref (get-x-text-property w 'WM_CLASS) 0))))
          (window-order current-workspace t nil))))
    (if windows
        (display-window (car windows))
      (system (concat mn-term-command " &")))))


;; Inspired by jump-or-exec written by Damien Elmes <resolve@repose.cx>
(defun mn-jump-or-exec (regexp command)
  (let ((w (get-window-by-name-re regexp)))
    (if w (display-window w) (system (concat command " &")))))

;; Menus
(setq apps-menu
      '(("_irssi"     (mn-jump-or-exec "^irssi$"  mn-irssi-command))
        ("_Emacs"     (mn-jump-or-exec "^Emacs"   "emacs"))
        ("The _GIMP"  (mn-jump-or-exec "^GIMP$"   "gimp"))
        ("GQ_View"    (mn-jump-or-exec "^GQview$" "gqview"))
        ("_Opera"     (mn-jump-or-exec "Opera$"   "opera")))
      root-menu
      (nconc
       '(("_Customize" customize)
         ("Re_start"   restart)
         ()
         ("_Quit"      quit)
         ("_Reboot"    (progn (system "( sleep 2; /sbin/reboot )") (quit))))
       (and (file-exists-p "/usr/local/sbin/reboot-w")
            '(("Reboot _Windows"
               (progn (system "( sleep 2; /usr/local/sbin/reboot-w )") (quit)))))
       '(("Power _Off" (progn (system "( sleep 2; /sbin/halt   )") (quit))))))


;; Application and menu bindings
(bind-keys root-window-keymap
           "Button1-Click2" (lambda () (system (concat mn-term-command " &")))
           "Button2-Click"  (lambda () (popup-menu window-menu))
           "Button3-Click"  popup-apps-menu
           "W-Space"        popup-apps-menu
           "W-Button3-Click"popup-root-menu)

(bind-keys global-keymap
           "W-`"            mn-focus-term
           "W-q"           (lambda()(mn-jump-or-exec "^Emacs"   "emacs"))
           "W-w"           (lambda()(mn-jump-or-exec "Opera$"   "opera"))
           "W-e"           (lambda()(mn-jump-or-exec "^irssi$"  mn-irssi-command))
           "W-r"           (lambda()(mn-jump-or-exec "^GIMP$"   "gimp"))
           "W-t"           (lambda()(mn-jump-or-exec "^GQview$" "gqview")))

(bind-keys window-keymap
           "W-Space"        (lambda () (popup-window-menu (input-focus))))
;; (bind-keys window-keymap
;;            "W-Button3-Click"(lambda () (popup-window-menu (input-focus))))


;; Run some applications
(add-hook 'after-initialization-hook
          (lambda ()
            (mapc (lambda (pair)
                    (unless (and (car pair)(get-window-by-name-re (car pair)))
                      (system (concat (cdr pair) " &"))))
                  (mn-val-filter mn-machine
                   '((erwin pikus)       "Opera"    . "opera")
                   '((erwin pikus)       "Emacs"    . "emacs")
                   `((erwin pikus)       "irssi"    . ,mn-irssi-command)
                   `((eval . mn-mpdshow-command)
                                         "mpdshow"  . ,mn-mpdshow-command)
                   '(()                  "gkrellm"  . "gkrellm")))
            (set-screen-viewport 0 0)))

;;}}}
;;{{{ Music Player Daemon

(when (member mn-machine '(erwin tuptus))
  (when (and (= mn-machine 'tuptus) (not (getenv "MPD_HOST")))
    (setenv "MPD_HOST" "erwin"))

  (defun mn-mpd (command)
    (system
     (concat "echo '" command
             "\nclose\n' >/dev/tcp/${MPD_HOST-localhost}/${MPD_PORT-6600}")))

  (defmacro mn-mpd-lambda (command)
    (list 'lambda () (list 'mn-mpd command)))

  (bind-keys global-keymap
             "W-z"            (mn-mpd-lambda "previous")
             "W-x"            (mn-mpd-lambda "play")
             "W-c"            (mn-mpd-lambda "pause")
             "W-v"            (mn-mpd-lambda "stop")
             "W-b"            (mn-mpd-lambda "next")))

;;}}}
;;{{{ Matcher

(require 'sawfish.wm.ext.match-window)
(setq match-window-profile
      (mn-val-filter (screen-size)
       '(("1680x1050") ((WM_CLASS . "^Opera/opera$"))
                       (ignore-program-position . #t)
                       (position 583 . 350) (dimensions 1096 . 700)
                       (viewport 1 . 1))
       '(("1024x768" ) ((WM_CLASS . "^Opera/opera$"))
                       (ignore-program-position . #t)
                       (position 0 . 368) (dimensions 1024 . 400)
                       (viewport 1 . 1))
       '(("1600x1200") ((WM_CLASS . "^Opera/opera$"))
                       (ignore-program-position . #t)
                       (position 600 . 500) (dimensions 1000 . 700)
                       (viewport 1 . 1))
       '(("1200x1600") ((WM_CLASS . "^Opera/opera$"))
                       (ignore-program-position . #t)
                       (position 0 . 0) (dimensions 1200 . 1600)
                       (viewport 2 . 1) (maximize . both))
       '(()            ((WM_ICON_NAME . "^GQview$"))
                       (viewport 4 . 1))
       '(("1680x1050") ((WM_CLASS . "^Emacs"))
                       (ignore-program-position . #t)
                       (position 0 . 0) (dimensions 80 . 87)
                       (viewport 1 . 1) (maximize . vertical))
       '(("1024x768" ) ((WM_CLASS . "^Emacs"))
                       (ignore-program-position . #t)
                       (position 0 . 0) (dimensions 80 . 76)
                       (viewport 1 . 1) (maximize . vertical))
       '(("1600x1200") ((WM_CLASS . "^Emacs"))
                       (ignore-program-position . #t)
                       (position 0 . 0) (dimensions 80 . 80)
                       (viewport 1 . 1) (maximize . vertical))
       '(("1200x1600") ((WM_CLASS . "^Emacs"))
                       (ignore-program-position . #t)
                       (position 0 . 0) (dimensions 80 . 106)
                       (viewport 1 . 1) (maximize . vertical))
       '(()            ((WM_CLASS . "^Gimp/gimp|MPlayer/.*|openttd/openttd$"))
                       (alpha . 1))
       '(()            ((WM_CLASS . "^Gimp/gimp$") (WM_WINDOW_ROLE . "^gimp-[^s]"))
                       (viewport 3 . 1) (group . Gimp))
       '(()            ((WM_CLASS . "^Gimp/gimp$") (WM_WINDOW_ROLE . "^gimp-startup$"))
                       (place-mode . centered) (depth . 16)
                       (sticky . #t) (sticky-viewport . #t)
                       (cycle-skip . #t) (window-list-skip . #t)
                       (task-list-skip . #t))
       '(()            ((WM_ICON_NAME . "^mpdshow$"))
                       (focus-when-mapped . #f) (focus-mode . click)
                       (sticky . #t) (sticky-viewport . #t)
                       (cycle-skip . #t) (window-list-skip . #t)
                       (task-list-skip . #t))
       '(()            ((WM_ICON_NAME . "^irssi$"))
                       (sticky . #t) (sticky-viewport . #t))))

;;}}}

; (restart)
